buildscript {
    ext.kotlin_version = '1.3-M1'
    ext.front_end_plugin_version = '0.0.33'
    ext.kotlinx_html_version = '0.6.11'
    ext.node_version = '10.7.0'
    ext.antlr_kotlin_version = '2b7630b'

    repositories {
        mavenCentral()
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:$front_end_plugin_version"
    }
}

group 'timpatterson'
version '1.0-SNAPSHOT'

apply plugin: 'antlr'
apply plugin: 'org.jetbrains.kotlin.frontend'
apply plugin: 'kotlin2js'
apply plugin: 'kotlin-dce-js'

generateGrammarSource {
    arguments += ["-Dlanguage=Kotlin"]
}

sourceSets {
    main {
        kotlin.srcDir 'build/generated-src/antlr/main'
        kotlin.srcDir 'src/main/kotlin'
    }
}

compileKotlin2Js {
    dependsOn generateGrammarSource

    kotlinOptions.metaInfo = true
    kotlinOptions.outputFile = "${project.buildDir.path}/js/${project.name}.js"
    kotlinOptions.sourceMap = true
    kotlinOptions.moduleKind = 'commonjs'
}

kotlinFrontend {
    downloadNodeJsVersion = node_version

    npm {
        dependency "style-loader"
        dependency "css-loader"
        dependency "foundation-sites"
    }

    webpackBundle {
        bundleName = "main"
        contentPath = file('src/main/web')
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    antlr "com.github.tim-patterson.antlr-kotlin:antlr-kotlin-target:$antlr_kotlin_version"
    compile "com.github.tim-patterson.antlr-kotlin:antlr-kotlin-runtime-js:$antlr_kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-html-js:$kotlinx_html_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}
